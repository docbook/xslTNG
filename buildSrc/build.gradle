plugins {
  id "java"
}

sourceCompatibility=1.8
targetCompatibility=1.8

repositories {
  mavenLocal()
  mavenCentral()
  gradlePluginPortal()
  maven { url "https://dev.saxonica.com/maven" }
}

apply from: "${rootDir.parentFile}/properties.gradle"

configurations.all {
  resolutionStrategy {
    exclude group: 'xml-apis', module: 'xml-apis'
    force "${saxonGroup}:${saxonEdition}:${saxonVersion}",
      "org.xmlresolver:xmlresolver:${xmlresolverVersion}"
  }
}

dependencies {
  implementation (
    [group: saxonGroup, name: saxonEdition, version: saxonVersion],
    [group: 'com.drewnoakes', name: 'metadata-extractor',
     version: metadataExtractorVersion],
    [group: 'org.xmlresolver', name: 'xmlresolver', version: xmlresolverVersion],
    [group: 'org.relaxng', name: 'jing', version: jingVersion ],
    [group: 'com.nwalsh', name: 'sinclude', version: sincludeVersion ],
    [group: 'org.docbook', name: 'schemas-docbook', version: docbookVersion],
    [group: 'org.docbook', name: 'schemas-publishers', version: docbookVersion],
    [group: 'com.nwalsh.gradle.saxon', name: 'saxon-gradle', version: '0.10.5'],
    [group: 'com.nwalsh.gradle.relaxng', name: 'relaxng-gradle', version: '0.10.3']
  )
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java', 'build/generated/sources/buildconfig']
    }
  }
}

// Cheap and cheerful alternative to the BuildConfig plugin
mkdir "${buildDir}/generated/sources/buildconfig/main/java/org/docbook/xsltng"
new File("${buildDir}/generated/sources/buildconfig/main/java/org/docbook/xsltng/BuildConfig.java")
  .withWriter("utf-8") { writer ->
    writer.writeLine("package org.docbook.xsltng;")
    writer.writeLine("// Generated code, do not edit")
    writer.writeLine("public final class BuildConfig {")
    writer.writeLine("  public static final String TITLE = \"${xslTNGtitle}\";")
    writer.writeLine("  public static final String VERSION = \"${xslTNGversion}\";")
    writer.writeLine("  public static final String SAXON_VERSION = \"${saxonVersion}\";")
    writer.writeLine("  private BuildConfig() {}")
    writer.writeLine("}")
  }

